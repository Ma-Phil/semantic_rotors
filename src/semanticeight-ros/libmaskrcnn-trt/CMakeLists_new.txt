# SPDX-FileCopyrightText: 2019 NVIDIA Corporation
# SPDX-FileCopyrightText: 2021-2023 Smart Robotics Lab, Imperial College London
# SPDX-FileCopyrightText: 2021-2023 Sotiris Papatheodorou
# SPDX-License-Identifier: Apache-2.0

cmake_minimum_required(VERSION 3.10..3.16 FATAL_ERROR)

project(libmaskrcnn-trt
    VERSION 1.0.0
    DESCRIPTION "Run Mask R-CNN using TensorRT"
    HOMEPAGE_URL "https://bitbucket.org/smartroboticslab/libmaskrcnn-trt"
    LANGUAGES CXX CUDA
)

option(BUILD_EXAMPLES "Compile the libmaskrcnn-trt examples" ON)

find_package(CUDA REQUIRED)
find_package(OpenCV REQUIRED COMPONENTS core imgproc)

# — Find TensorRT core, plugin, ONNX & UFF parsers —
find_path(TENSORRT_INCLUDE_DIR
  NAMES NvInfer.h NvOnnxParser.h NvUffParser.h
  PATHS /usr/include /usr/include/x86_64-linux-gnu
)

find_library(NVINFER_LIB        nvinfer
  PATHS /usr/lib /usr/lib/x86_64-linux-gnu
)
find_library(NVINFER_PLUGIN_LIB nvinfer_plugin)
find_library(NVONNXPARSER_LIB   nvinfer_onnxparser)
find_library(NVPARSERS_LIB      nvparsers)

# If any of the core or parser libs are missing, fail
# if(NOT TENSORRT_INCLUDE_DIR OR NOT NVINFER_LIB OR NOT NVINFER_PLUGIN_LIB OR NOT NVONNXPARSER_LIB OR NOT NVPARSERS_LIB)
#   message(FATAL_ERROR "Could not find TensorRT headers or libraries.")
# endif()

include_directories(${TENSORRT_INCLUDE_DIR})

# CUDA setup ###################################################################
if(DEFINED GPU_ARCHS)
    message(STATUS "GPU_ARCHS defined as ${GPU_ARCHS}.")
    separate_arguments(GPU_ARCHS)
else()
    list(APPEND GPU_ARCHS 35 53 61 70 75)
    if(CMAKE_HOST_SYSTEM_PROCESSOR STREQUAL "aarch64")
        list(APPEND GPU_ARCHS 72)
    endif()
    if(CUDA_VERSION VERSION_GREATER_EQUAL "11.0")
        list(APPEND GPU_ARCHS 80)
    endif()
    message(STATUS "GPU_ARCHS is not defined, using defaults: ${GPU_ARCHS}.")
endif()
message(STATUS "Generating CUDA code for SM ${GPU_ARCHS}")
set(BERT_GENCODES)
foreach(arch ${GPU_ARCHS})
    if(${arch} GREATER_EQUAL 70)
        set(BERT_GENCODES "${BERT_GENCODES} -gencode arch=compute_${arch},code=sm_${arch}")
    endif()
    set(GENCODES "${GENCODES} -gencode arch=compute_${arch},code=sm_${arch}")
endforeach()
list(GET GPU_ARCHS -1 LATEST_SM)
set(GENCODES "${GENCODES} -gencode arch=compute_${LATEST_SM},code=compute_${LATEST_SM}")
if(${LATEST_SM} GREATER_EQUAL 70)
    set(BERT_GENCODES "${BERT_GENCODES} -gencode arch=compute_${LATEST_SM},code=compute_${LATEST_SM}")
endif()

if(CUDA_VERSION VERSION_GREATER "10.0")
    find_library(CUBLASLT_LIB cublasLt HINTS ${CUDA_TOOLKIT_ROOT_DIR} PATH_SUFFIXES lib64 lib lib/stubs)
else()
    message(WARNING "CUDA version is <= 10.0, disabling BERT plugins.")
    unset(BERT_GENCODES)
endif()

find_library(CUDNN_LIB   cudnn   HINTS ${CUDA_TOOLKIT_ROOT_DIR} ${CUDNN_ROOT_DIR} PATH_SUFFIXES lib64 lib)
find_library(CUBLAS_LIB  cublas  HINTS ${CUDA_TOOLKIT_ROOT_DIR} PATH_SUFFIXES lib64 lib lib/stubs)
find_library(CUDART_LIB  cudart  HINTS ${CUDA_TOOLKIT_ROOT_DIR} PATH_SUFFIXES lib lib64)
find_library(RT_LIB      rt)
include(${CMAKE_CURRENT_SOURCE_DIR}/cmake/modules/find_library_create_target.cmake)
find_library_create_target(nvinfer         nvinfer         SHARED ${CMAKE_BINARY_DIR})
find_library_create_target(nvinfer_plugin  nvinfer_plugin  SHARED ${CMAKE_BINARY_DIR})
find_library_create_target(nvparsers       nvparsers       SHARED ${CMAKE_BINARY_DIR})

set(CMAKE_CXX_FLAGS   "-Wno-deprecated-declarations ${CMAKE_CXX_FLAGS} -DBUILD_SYSTEM=cmake_oss")
set(CMAKE_CUDA_FLAGS "${CMAKE_CUDA_FLAGS} -Xcompiler -Wno-deprecated-declarations")

# libmaskrcnn-trt ##############################################################
set(LIB_NAME maskrcnn-trt)
add_library(${LIB_NAME} STATIC
    src/logger.cpp
    src/maskrcnn_config.cpp
    src/detection.cpp
    src/maskrcnn.cpp
)
target_include_directories(${LIB_NAME}
    PUBLIC
        include
        ${CUDA_INCLUDE_DIRS}
        ${CUDNN_ROOT_DIR}/include
        ${OpenCV_INCLUDE_DIRS}
)

target_link_libraries(${LIB_NAME}
    ${CUDART_LIB}
    ${CUBLAS_LIB}
    ${CUDNN_LIB}
    ${NVINFER_LIB}
    ${NVINFER_PLUGIN_LIB}
    ${NVPARSERS_LIB}
    ${NVONNXPARSER_LIB}
    ${OpenCV_LIBS}
    ${RT_LIB}
    ${CMAKE_DL_LIBS}
    ${CMAKE_THREAD_LIBS_INIT}
    stdc++fs
    -Wl,--unresolved-symbols=ignore-in-shared-libs
)
target_compile_features(${LIB_NAME} PUBLIC cxx_std_17)
set_target_properties(${LIB_NAME}
    PROPERTIES
        LINK_FLAGS "-Wl,--exclude-libs,ALL"
        ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}"
        LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}"
)

# Executables ##################################################################
if(BUILD_EXAMPLES)
    find_package(OpenCV REQUIRED COMPONENTS core imgproc imgcodecs videoio highgui)

    add_executable(${LIB_NAME}-example src/maskrcnn_example.cpp)
    target_link_libraries(${LIB_NAME}-example ${LIB_NAME} ${OpenCV_LIBS})

    add_executable(${LIB_NAME}-camera src/maskrcnn_camera.cpp)
    target_link_libraries(${LIB_NAME}-camera ${LIB_NAME} ${OpenCV_LIBS})
endif()
